plugins {
    id('java')
    id('jacoco')
    id('checkstyle')
    id('distribution')
    id('maven')
    id('groovy')
    id('org.omegat.gradle') version '1.2.4'
}

version = '0.1'

omegat {
    version = "5.2.0"
    pluginClass = pluginMainClass
}

dependencies {
    packIntoJar  'org.slf4j:slf4j-api:1.7.21'
    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'org.slf4j:slf4j-nop:1.7.21'
    testImplementation 'junit:junit:4.12'
    testImplementation 'xmlunit:xmlunit:1.6'
    testImplementation 'org.madlonkay.supertmxmerge:supertmxmerge:2.0.1'
}

checkstyle {
    config = resources.text.fromFile("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = true
    toolVersion = '7.1'
}
tasks.checkstyleMain.setGroup('Verification')

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

javadoc {
    options.locale = 'en_US'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

distTar {
  compression = Compression.GZIP
}

distributions {
    main {
        contents {
            from('README.md', 'COPYING')
            into('docs') {
                from 'CHANGELOG.md'
            }
            from(sourceJar) {
                into 'source'
            }
            from(jar)
        }
    }
}
